plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao'
}

// 加载密钥库配置
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
// 优先从环境变量中读取签名配置
def useEnvVariables = System.getenv('ZEROTIERFIX_KEYSTORE_PASSWORD') != null

if (useEnvVariables) {
    println "使用环境变量中的签名配置"
    keystoreProperties.put('storeFile', System.getenv('ZEROTIERFIX_KEYSTORE_FILE') ?: 'zerotierfix.keystore')
    keystoreProperties.put('storePassword', System.getenv('ZEROTIERFIX_KEYSTORE_PASSWORD'))
    keystoreProperties.put('keyAlias', System.getenv('ZEROTIERFIX_KEY_ALIAS'))
    keystoreProperties.put('keyPassword', System.getenv('ZEROTIERFIX_KEY_PASSWORD'))
} else if (keystorePropertiesFile.exists()) {
    println "使用本地keystore.properties中的签名配置"
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

greendao {
    schemaVersion 22
    daoPackage 'net.kaaass.zerotierfix.model'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "net.kaaass.zerotierfix"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 14
        versionName "1.0.10"
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    // 添加签名配置
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists() || useEnvVariables) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // 应用签名配置
            if (keystorePropertiesFile.exists() || useEnvVariables) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }

    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'commons-logging'
        }
    }
    namespace 'net.kaaass.zerotierfix'
}

dependencies {
    implementation project(':core')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'commons-io:commons-io:2.11.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation "androidx.multidex:multidex:2.0.1"
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}